from synch import *

def RollerCoaster(nseats): result = {
    .mutex: Lock(), .nseats: nseats, .entered: 0, .left: nseats,
    .empty: Condition(), .full: Condition()
}

def enter(b):
    acquire(?b->mutex)
    while b->entered == b->nseats:  # wait for car to empty out
        wait(?b->empty, ?b->mutex)
    b->entered += 1
    if b->entered != b->nseats:     # wait for car to fill up
        while b->entered < b->nseats:
            wait(?b->full, ?b->mutex)
    else:                           # car is ready to go
        b->left = 0
        notify_all(?b->full)         # wake up others waiting in car
    release(?b->mutex)

def exit(b):
    acquire(?b->mutex)
    assert b->entered == b->nseats
    b->left += 1
    if b->left == b->nseats:    # car is empty
        b->entered = 0
        # notify_all(?b->empty)    # wake up riders wanting to go
        notify(?b->empty)
        notify(?b->empty)
        notify(?b->empty)
        notify(?b->empty)
    release(?b->mutex)

const NSEATS = 4
const NTHREADS = 8

therc = RollerCoaster(NSEATS)

def student():
    enter(?therc)
    exit(?therc)

for _ in { 1 .. NTHREADS }:
    spawn student()
