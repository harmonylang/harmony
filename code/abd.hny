import bag

const F = 1
const N = (2 * F) + 1

network = bag.empty()

def send(m):
    atomically network = bag.add(network, m)

def server():
    var t, v, received = (0, None), None, {}
    while True:
        atomically when exists m in { m for m in keys network - received
                            where m.type in {.read, .write} }:
            received |= { m }
            if (m.type == .write) and (m.value[0] > t):
                t, v = m.value
            send({ .type: .response, .dst: m.src, .value: (t, v) })

def receive(uid, phase):
    let msgs = { m:c for m:c in network
                where (m.type == .response) and (m.dst == (uid, phase)) }:
        result = bag.combinations(msgs, N - F)

def read(uid):
    send({ .type: .read, .src: (uid, 1) })
    atomically when exists msgs in receive(uid, 1):
        let (t, v) = max(m.value for m in keys msgs):
            send({ .type: .write, .src: (uid, 2), .value: (t, v) })
            result = v
    atomically when exists msgs in receive(uid, 2):
        pass

def write(uid, v):
    send({ .type: .read, .src: (uid, 1) })
    atomically when exists msgs in receive(uid, 1):
        let (t, _) = max(m.value for m in keys msgs)
        let nt = (t[0] + 1, uid):
            send({ .type: .write, .src: (uid, 2), .value: (nt, v) })
    atomically when exists msgs in receive(uid, 2):
        pass
