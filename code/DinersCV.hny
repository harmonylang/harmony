import synch;

const N = 5;

mutex = synch.Lock();
forks = [False,] * N;
conds = [synch.Condition(?mutex),] * N;

def diner(which):
    let left, right = (which - 1, which % N):
        while choose({ False, True }):
            synch.acquire(?mutex);
            while forks[left] or forks[right]:
                if forks[left]:
                    synch.wait(?conds[left], ?mutex);
                ;
                if forks[right]:
                    synch.wait(?conds[right], ?mutex);
                ;
            ;
            assert not (forks[left] or forks[right]);
            forks[left] = forks[right] = True;
            synch.release(?mutex);

            # dine

            synch.acquire(?mutex);
            forks[left] = forks[right] = False;
            synch.notify(?conds[left]); synch.notify(?conds[right]);
            synch.release(?mutex);

            # think
        ;
    ;
;

for i in {1..N}:
    spawn diner(i);
;
