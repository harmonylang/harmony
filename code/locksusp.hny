import list

def BinSema(acquired):
    result = dict{ .acquired: acquired, .suspended: [] }

def Lock():
    result = BinSema(False)

def acquired(binsema):
    result = binsema->acquired

def acquire(binsema):
    atomic:
        if binsema->acquired:
            stop binsema->suspended
            assert binsema->acquired
        else:
            binsema->acquired = True

def release(binsema):
    atomic:
        assert binsema->acquired
        if binsema->suspended == []:
            binsema->acquired = False
        else:
            go (list.head(binsema->suspended)) ()
            binsema->suspended = list.tail(binsema->suspended)
        
def held(binsema):
    result = binsema->acquired
