def Qsort(arr):
    result = dict{ .arr: arr, .todo: { (0, len(arr) - 1) } };
;
def swap(p, q):               # swap !p and !q
    !p, !q = !q, !p; 
;
def partition(qs, lo, hi):
    result = lo;
    for i in {lo..hi - 1}:
        if qs->arr[i] <= qs->arr[hi]:
            swap(?qs->arr[result], ?qs->arr[i]);
            result += 1;
        ;
    ;
    swap(?qs->arr[result], ?qs->arr[hi]); 
;
def sortrange(qs, range):
    let lo, hi = range let pivot = partition(qs, lo, hi):
        if (pivot - 1) > lo:
            qs->todo |= { (lo, pivot - 1) };
        ;
        if (pivot + 1) < hi:
            qs->todo |= { (pivot + 1, hi) };
        ;
    ;
;
def qsort(qs):
    while qs->todo != {}:
        let range = choose(qs->todo):
            qs->todo -= { range };
            sortrange(qs, range);
        ;
    ;
    result = qs->arr;
;
